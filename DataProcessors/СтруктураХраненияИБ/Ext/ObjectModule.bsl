//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ:	Обработка для получения структуры хранения базы данных 																		//
// ВЕРСИЯ:		14.09.2017																													//
// АВТОР:		Усевич Алексей                                         																		//
// САЙТ:		www.ausevich.ru                                       																		//
// СТРАНИЦА:	http://ausevich.ru/ekspert/poluchenie-informatsii-o-strukture-khraneniya-bazy-dannykh-v-terminakh-1s-predpriyatie-i-subd/ 	//
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ПреобразоватьВДерево(Таблица) Экспорт
	
	ДеревоЗначений = Новый ДеревоЗначений;
	
	Таблица.Колонки.Добавить("ВидМетаданного", Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл 
		СтрокаТаблицы.ВидМетаданного = Лев(СтрокаТаблицы.Метаданные, Найти(СтрокаТаблицы.Метаданные, ".") - 1);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТ_Таблица.ИмяТаблицы,
	               |	ВТ_Таблица.ИмяТаблицыХранения,
	               |	ВЫРАЗИТЬ(ВТ_Таблица.Метаданные КАК СТРОКА(256)) КАК Метаданные,
	               |	ВЫРАЗИТЬ(ВТ_Таблица.ВидМетаданного КАК СТРОКА(256)) КАК ВидМетаданного,
	               |	ВТ_Таблица.Назначение
	               |ПОМЕСТИТЬ ВТ_Таблица
	               |ИЗ
	               |	&Таблица КАК ВТ_Таблица
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Таблица.ИмяТаблицы,
	               |	ВТ_Таблица.ИмяТаблицыХранения,
	               |	ВТ_Таблица.Метаданные КАК Метаданные,
	               |	ВТ_Таблица.Назначение,
	               |	ВТ_Таблица.ВидМетаданного КАК ВидМетаданного
	               |ИЗ
	               |	ВТ_Таблица КАК ВТ_Таблица
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВидМетаданного,
	               |	Метаданные
	               |ИТОГИ ПО
	               |	ВидМетаданного,
	               |	Метаданные";
					
	Запрос.УстановитьПараметр("Таблица", Таблица);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ДеревоЗначений = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);	
	КонецЕсли;
	
	Возврат ДеревоЗначений;
	
КонецФункции